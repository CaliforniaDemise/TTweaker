
repositories {
    maven {
        name 'CleanroomMC Maven'
        url 'https://maven.cleanroommc.com'
    }
    maven {
        name 'CurseMaven'
        url 'https://curse.cleanroommc.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.1'

    var jei = 'mezz:jei:4.25.5'
    var ct = 'curse.maven:crafttweaker-239197:4566528'
    var grs = 'com.cleanroommc:groovyscript:1.1.3'
    var psi = 'curse.maven:psi-241665:3085917'
    var pizzacraft = 'curse.maven:pizzacraft-292056:2986635'

    var mixins = project.use_groovyscript.toBoolean()

    // JEI
    if (project.use_jei.toBoolean()) implementation rfg.deobf(jei)
    else compileOnly rfg.deobf(jei)

    // CraftTweaker
    if (project.use_crafttweaker.toBoolean()) implementation(ct)
    else compileOnly(ct)

    // GroovyScript
    if (project.use_groovyscript.toBoolean()) implementation(grs) { transitive = false }
    else compileOnly(grs) { transitive = false }

    // Psi
    if (project.use_psi.toBoolean()) {
        implementation('curse.maven:autoreglib-250363:2746011')
        implementation rfg.deobf(psi)
    }
    else compileOnly rfg.deobf(psi)

    // Pizzacraft
    if (project.use_pizzacraft.toBoolean()) implementation rfg.deobf(pizzacraft)
    else compileOnly rfg.deobf(pizzacraft)

    // Mixins
    if (mixins) {
        annotationProcessor 'org.ow2.asm:asm-debug-all:5.2'
        annotationProcessor 'com.google.guava:guava:32.1.2-jre'
        annotationProcessor 'com.google.code.gson:gson:2.8.9'

        String mixinBooter = modUtils.enableMixins('zone.rong:mixinbooter:9.1')
        api (mixinBooter) { transitive = false }
        annotationProcessor (mixinBooter) { transitive = false }
    }
}